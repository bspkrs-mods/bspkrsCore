import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: "forge"

compileJava.options.encoding = 'UTF-8'

// define the properties file
ext.configFile = file "build.properties"

configFile.withReader {
    // read config.  it shall from now on be referenced as simply config or as project.config
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}

group = config.group_name
version = config.mod_version
archivesBaseName = "[${config.minecraft_version}]${config.mod_id}"

minecraft {
    version = config.minecraft_version + "-" + config.forge_version // grab latest forge
    assetDir = "run/assets"
}

compileJava.source = 'build/sources/java'

task updateTokens(type:Copy){
  from(sourceSets.main.java)
  {
    include '**/*.java'    
  }  
  into 'build/sources/java'
  filter(ReplaceTokens, tokens: [MINECRAFT_VERSION: config.minecraft_version, MOD_VERSION: config.mod_version])
}

compileJava.dependsOn updateTokens

processResources {
    // replace stuff in the files we want.
    from(sourceSets.main.resources.srcDirs) {
        include '**/*.info'
        include '**/*.properties'

        // replaces
        expand ([
            'mod_version': config.mod_version,
            'forge_version': config.forge_version,
            'minecraft_version': config.minecraft_version,
        ])
    }

    // copy everything else that we didnt do before
    from(sourceSets.main.resources.srcDirs) {
        exclude '**/*.info'
        exclude '**/*.properties'
    }
}

// change the name of my obfuscated jar
jar {
    appendix = 'universal'
}

// add a source jar
task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    appendix = 'src'
}

// because the normal output has been made to be obfuscated
task deobfJar(type: Jar) {
    from sourceSets.main.output
    appendix = 'deobf'
}

artifacts {
    archives sourceJar
    archives deobfJar
}